import eei::*;
import corectrl::*;

module memunit (
    clk   : input   clock                                   ,
    rst   : input   reset                                   ,
    valid : input   logic                                   ,
    is_new: input   logic                                   , // 命令が新しく供給されたかどうか
    ctrl  : input   InstCtrl                                , // 命令のInstCtrl
    addr  : input   Addr                                    , // アクセスするアドレス
    rs2   : input   UIntX                                   , // ストア命令で書き込むデータ
    rdata : output  UIntX                                   , // ロード命令の結果 (stall = 0のときに有効)
    stall : output  logic                                   , // メモリアクセス命令が完了していない
    membus: modport membus_if::<MEM_DATA_WIDTH, XLEN>::master, // メモリとのinterface
) {

    // 命令がメモリにアクセスする命令か判別する関数
    function inst_is_memop (
        ctrl: input InstCtrl,
    ) -> logic    {
        return ctrl.itype == InstType::S || ctrl.is_load;
    }

    // 命令がストア命令か判別する関数
    function inst_is_store (
        ctrl: input InstCtrl,
    ) -> logic    {
        return inst_is_memop(ctrl) && !ctrl.is_load;
    }

    // memunitの状態を表す列挙型
    enum State: logic<2> {
        Init, // 命令を受け付ける状態
        WaitReady, // メモリが操作可能になるのを待つ状態
        WaitValid, // メモリ操作が終了するのを待つ状態
    }

    var state: State;

    var req_wen  : logic                    ;
    var req_addr : Addr                     ;
    var req_wdata: logic<MEM_DATA_WIDTH>    ;
    var req_wmask: logic<MEM_DATA_WIDTH / 8>;

    const W   : u32                   = XLEN;
    let D   : logic<MEM_DATA_WIDTH> = membus.rdata;
    let sext: logic                 = ctrl.funct3[2] == 1'b0;

    always_comb {
        // メモリアクセス
        membus.valid = state == State::WaitReady;
        membus.addr  = req_addr;
        membus.wen   = req_wen;
        membus.wdata = req_wdata;
        membus.wmask = req_wmask;
        // loadの結果
        rdata = case ctrl.funct3[1:0] {
            2'b00  : case addr[1:0] {
                0      : {sext & D[7] repeat W - 8, D[7:0]},
                1      : {sext & D[15] repeat W - 8, D[15:8]},
                2      : {sext & D[23] repeat W - 8, D[23:16]},
                3      : {sext & D[31] repeat W - 8, D[31:24]},
                default: 'x,
            },
            2'b01  : case addr[1:0] {
                0      : {sext & D[15] repeat W - 16, D[15:0]},
                2      : {sext & D[31] repeat W - 16, D[31:16]},
                default: 'x,
            },
    // #@@range_begin(lw)
    2'b10  : @<b>|{D[31] repeat W - 32, D[31:0]}|,
    // #@@range_end(lw)
            default: 'x,
        };
        // stall判定
        stall = valid & case state {
            State::Init     : is_new && inst_is_memop(ctrl),
            State::WaitReady: 1,
            State::WaitValid: !membus.rvalid,
            default         : 0,
        };
    }

    always_ff {
        if_reset {
            state     = State::Init;
            req_wen   = 0;
            req_addr  = 0;
            req_wdata = 0;
            req_wmask = 0;
        } else {
            if valid {
                case state {
                    State::Init: if is_new & inst_is_memop(ctrl) {
                                     state     = State::WaitReady;
                                     req_wen   = inst_is_store(ctrl);
                                     req_addr  = addr;
                                     req_wdata = rs2 << {addr[1:0], 3'b0};
                                     req_wmask = case ctrl.funct3[1:0] {
                                         2'b00  : 4'b1 << addr[1:0],
                                         2'b01  : case addr[1:0] {
                                             2      : 4'b1100,
                                             0      : 4'b0011,
                                             default: 'x,
                                         },
                                         2'b10  : 4'b1111,
                                         default: 'x,
                                     };
                                 }
                    State::WaitReady: if membus.ready {
                                          state = State::WaitValid;
                                      }
                    State::WaitValid: if membus.rvalid {
                                          state = State::Init;
                                      }
                    default: {}
                }
            }
        }
    }
}

import eei::*;

module mmio_controller (
    clk          : input   clock         ,
    rst          : input   reset         ,
    DBG_ADDR     : input   Addr          ,
    req_core     : modport Membus::slave ,
    ram_membus   : modport Membus::master,
    rom_membus   : modport Membus::master,
    dbg_membus   : modport Membus::master,
    aclint_membus: modport Membus::master,
) {

    enum Device {
        UNKNOWN,
        RAM,
        ROM,
        DEBUG,
        ACLINT,
    }

    inst req_saved: Membus;

    var last_device : Device;
    var is_requested: logic ;

    // masterを0でリセットする
    function reset_membus_master (
        master: modport Membus::master_output,
    ) {
        master.valid = 0;
        master.addr  = 0;
        master.wen   = 0;
        master.wdata = 0;
        master.wmask = 0;
    }

    // すべてのデバイスのmasterをリセットする
    function reset_all_device_masters () {
        reset_membus_master(ram_membus);
        reset_membus_master(rom_membus);
        reset_membus_master(dbg_membus);
        reset_membus_master(aclint_membus);
    }

    // アドレスからデバイスを取得する
    function get_device (
        addr: input Addr,
    ) -> Device {
        if DBG_ADDR <= addr && addr <= DBG_ADDR + 7 {
            return Device::DEBUG;
        }
        if MMAP_ROM_BEGIN <= addr && addr <= MMAP_ROM_END {
            return Device::ROM;
        }
        if MMAP_ACLINT_BEGIN <= addr && addr <= MMAP_ACLINT_END {
            return Device::ACLINT;
        }
        if addr >= MMAP_RAM_BEGIN {
            return Device::RAM;
        }
        return Device::UNKNOWN;
    }

    // デバイスのmasterにreqの情報を割り当てる
    function assign_device_master (
        req: modport Membus::all_input,
    ) {
        case get_device(req.addr) {
            Device::RAM: {
                ram_membus      <> req;
                ram_membus.addr -= MMAP_RAM_BEGIN;
            }
            Device::ROM: {
                rom_membus      <> req;
                rom_membus.addr -= MMAP_ROM_BEGIN;
            }
            Device::DEBUG: {
                dbg_membus      <> req;
                dbg_membus.addr -= DBG_ADDR;
            }
            Device::ACLINT: {
                aclint_membus      <> req;
                aclint_membus.addr -= MMAP_ACLINT_BEGIN;
            }
            default: {}
        }
    }

    // デバイスのrvalid、rdataをreqに割り当てる
    function assign_device_slave (
        device: input   Device          ,
        req   : modport Membus::response,
    ) {
        req.rvalid = 1;
        req.rdata  = 0;
        case device {
            Device::RAM   : req <> ram_membus;
            Device::ROM   : req <> rom_membus;
            Device::DEBUG : req <> dbg_membus;
            Device::ACLINT: req <> aclint_membus;
            default       : {}
        }
    }

    // デバイスのreadyを取得する
    function get_device_ready (
        device: input Device,
    ) -> logic {
        case device {
            Device::RAM   : return ram_membus.ready;
            Device::ROM   : return rom_membus.ready;
            Device::DEBUG : return dbg_membus.ready;
            Device::ACLINT: return aclint_membus.ready;
            default       : {}
        }
        return 1;
    }

    // デバイスのrvalidを取得する
    function get_device_rvalid (
        device: input Device,
    ) -> logic {
        case device {
            Device::RAM   : return ram_membus.rvalid;
            Device::ROM   : return rom_membus.rvalid;
            Device::DEBUG : return dbg_membus.rvalid;
            Device::ACLINT: return aclint_membus.rvalid;
            default       : {}
        }
        return 1;
    }

    // req_coreの割り当て
    // #@@range_begin(comb)
    always_comb {
        req_core.ready  = 0;
        req_core.rvalid = 0;
        req_core.rdata  = 0;
        req_core.expt   = 0;
    // #@@range_end(comb)

        if req_saved.valid {
            if is_requested {
                // 結果を返す
                assign_device_slave(last_device, req_core);
                req_core.ready      = get_device_rvalid(last_device);
            }
        } else {
            req_core.ready = 1;
        }
    }

    // デバイスのmasterの割り当て
    always_comb {
        reset_all_device_masters();
        if req_saved.valid {
            if is_requested {
                if get_device_rvalid(last_device) {
                    // 新しく要求を受け入れる
                    if req_core.ready && req_core.valid {
                        assign_device_master(req_core);
                    }
                }
            } else {
                // デバイスにreq_savedを割り当てる
                assign_device_master(req_saved);
            }
        } else {
            // 新しく要求を受け入れる
            if req_core.ready && req_core.valid {
                assign_device_master(req_core);
            }
        }
    }

    // 新しく要求を受け入れる
    function accept_request () {
        req_saved.valid = req_core.ready && req_core.valid;
        if req_core.ready && req_core.valid {
            last_device  = get_device(req_core.addr);
            is_requested = get_device_ready(last_device);
            // reqを保存
            req_saved.addr  = req_core.addr;
            req_saved.wen   = req_core.wen;
            req_saved.wdata = req_core.wdata;
            req_saved.wmask = req_core.wmask;
        }
    }

    function on_clock () {
        if req_saved.valid {
            if is_requested {
                if get_device_rvalid(last_device) {
                    accept_request();
                }
            } else {
                is_requested = get_device_ready(last_device);
            }
        } else {
            accept_request();
        }
    }

    function on_reset () {
        last_device         = Device::UNKNOWN;
        is_requested        = 0;
        reset_membus_master(req_saved);
    }

    always_ff {
        if_reset {
            on_reset();
        } else {
            on_clock();
        }
    }
}

import eei::*;

module top #(
    param RAM_FILEPATH_IS_ENV: bit    = 1              ,
    param RAM_FILEPATH       : string = "RAM_FILE_PATH",
    param ROM_FILEPATH_IS_ENV: bit    = 1              ,
    param ROM_FILEPATH       : string = "ROM_FILE_PATH",
) (
    clk          : input clock,
    rst          : input reset,
    MMAP_DBG_ADDR: input Addr ,
    #[ifdef(TEST_MODE)]
    test_success: output bit  ,
    led         : output UIntX,
) {

    // アドレスをデータ単位でのアドレスに変換する
    function addr_to_memaddr::<DATA_WIDTH: u32, ADDR_WIDTH: u32> (
        addr: input logic<XLEN>,
    ) -> logic<ADDR_WIDTH> {
        return addr[$clog2(DATA_WIDTH / 8)+:ADDR_WIDTH];
    }

    inst mmio_membus    : Membus;
    inst mmio_ram_membus: Membus;
    inst mmio_rom_membus: Membus;
    inst ram_membus     : membus_if::<RAM_DATA_WIDTH, RAM_ADDR_WIDTH>;
    inst rom_membus     : membus_if::<ROM_DATA_WIDTH, ROM_ADDR_WIDTH>;
    inst dbg_membus     : Membus;
    inst aclint_membus  : Membus;

    inst aclint_core_bus: aclint_if;

    // 命令フェッチ用
    inst i_membus     : Membus;
    inst i_membus_core: core_inst_if;
    // ロードストア命令用
    inst d_membus     : Membus;
    inst d_membus_core: core_data_if;

    var memarb_last_i: logic;

    // デバッグ用のIO
    always_ff {
        dbg_membus.ready  = 1;
        dbg_membus.rvalid = dbg_membus.valid;
        if dbg_membus.valid {
            if dbg_membus.wen {
                if dbg_membus.wdata[MEMBUS_DATA_WIDTH - 1-:20] == 20'h01010 {
                    $write("%c", dbg_membus.wdata[7:0]);
                } else if dbg_membus.wdata[lsb] == 1'b1 {
                    #[ifdef(TEST_MODE)]
                    {
                        test_success = dbg_membus.wdata == 1;
                    }
                    if dbg_membus.wdata == 1 {
                        $display("test success!");
                    } else {
                        $display("test failed!");
                        $error  ("wdata : %h", dbg_membus.wdata);
                    }
                    $finish();
                }
            } else {
                #[ifdef(ENABLE_DEBUG_INPUT)]
                {
                    dbg_membus.rdata = util::get_input();
                }
            }
        }
    }

    // mmio_controllerへのメモリアクセスを調停する
    always_ff {
        if_reset {
            memarb_last_i = 0;
        } else {
            if mmio_membus.ready {
                memarb_last_i = !d_membus.valid;
            }
        }
    }

    always_comb {
        i_membus.ready  = mmio_membus.ready && !d_membus.valid;
        i_membus.rvalid = mmio_membus.rvalid && memarb_last_i;
        i_membus.rdata  = mmio_membus.rdata;

        d_membus.ready  = mmio_membus.ready;
        d_membus.rvalid = mmio_membus.rvalid && !memarb_last_i;
        d_membus.rdata  = mmio_membus.rdata;

        mmio_membus.valid = i_membus.valid | d_membus.valid;
        if d_membus.valid {
            mmio_membus.addr  = d_membus.addr;
            mmio_membus.wen   = d_membus.wen;
            mmio_membus.wdata = d_membus.wdata;
            mmio_membus.wmask = d_membus.wmask;
        } else {
            mmio_membus.addr  = i_membus.addr;
            mmio_membus.wen   = 0; // 命令フェッチは常に読み込み
            mmio_membus.wdata = 'x;
            mmio_membus.wmask = 'x;
        }
    }

    always_comb {
        // mmio <> RAM
        ram_membus.valid       = mmio_ram_membus.valid;
        mmio_ram_membus.ready  = ram_membus.ready;
        ram_membus.addr        = addr_to_memaddr::<RAM_DATA_WIDTH, RAM_ADDR_WIDTH>(mmio_ram_membus.addr);
        ram_membus.wen         = mmio_ram_membus.wen;
        ram_membus.wdata       = mmio_ram_membus.wdata;
        ram_membus.wmask       = mmio_ram_membus.wmask;
        mmio_ram_membus.rvalid = ram_membus.rvalid;
        mmio_ram_membus.rdata  = ram_membus.rdata;
    }

    always_comb {
        // mmio <> ROM
        rom_membus.valid       = mmio_rom_membus.valid;
        mmio_rom_membus.ready  = rom_membus.ready;
        rom_membus.addr        = addr_to_memaddr::<ROM_DATA_WIDTH, ROM_ADDR_WIDTH>(mmio_rom_membus.addr);
        rom_membus.wen         = 0;
        rom_membus.wdata       = 0;
        rom_membus.wmask       = 0;
        mmio_rom_membus.rvalid = rom_membus.rvalid;
        mmio_rom_membus.rdata  = rom_membus.rdata;
    }

    inst ram: memory::<RAM_DATA_WIDTH, RAM_ADDR_WIDTH> #(
        FILEPATH_IS_ENV: RAM_FILEPATH_IS_ENV,
        FILEPATH       : RAM_FILEPATH       ,
    ) (
        clk               ,
        rst               ,
        membus: ram_membus,
    );

    inst rom: memory::<ROM_DATA_WIDTH, ROM_ADDR_WIDTH> #(
        FILEPATH_IS_ENV: ROM_FILEPATH_IS_ENV,
        FILEPATH       : ROM_FILEPATH       ,
    ) (
        clk               ,
        rst               ,
        membus: rom_membus,
    );

    inst aclintm: aclint_memory (
        clk                    ,
        rst                    ,
        membus: aclint_membus  ,
        aclint: aclint_core_bus,
    );

    inst mmioc: mmio_controller (
        clk                           ,
        rst                           ,
        DBG_ADDR     : MMAP_DBG_ADDR  ,
        req_core     : mmio_membus    ,
        ram_membus   : mmio_ram_membus,
        rom_membus   : mmio_rom_membus,
        dbg_membus                    ,
        aclint_membus                 ,
    );

    inst amou: amounit (
        clk                  ,
        rst                  ,
        slave : d_membus_core,
        master: d_membus     ,
    );

    inst fetcher: inst_fetcher (
        clk                   ,
        rst                   ,
        core_if: i_membus_core,
        mem_if : i_membus     ,
    );

    inst c: core (
        clk                      ,
        rst                      ,
        i_membus: i_membus_core  ,
        d_membus: d_membus_core  ,
        led                      ,
        aclint  : aclint_core_bus,
    );
}

import eei::*;

module top (
    clk          : input clock ,
    rst          : input reset ,
    MEM_FILE_PATH: input string,
) {

    // アドレスをメモリのデータ単位でのアドレスに変換する
    function addr_to_memaddr (
        addr: input Addr,
    ) -> Addr {
        return addr >> $clog2(MEM_DATA_WIDTH / 8);
    }

    inst membus  : membus_if::<MEM_DATA_WIDTH, XLEN>;
    inst i_membus: membus_if::<ILEN, XLEN>; // 命令フェッチ用
    inst d_membus: membus_if::<MEM_DATA_WIDTH, XLEN>; // ロードストア命令用

    // #@@range_begin(last_iaddr)
    var memarb_last_i    : logic;
    @<b>|var memarb_last_iaddr: Addr ;|
    // #@@range_end(last_iaddr)

    // riscv-testsの終了を検知する
    const RISCVTESTS_TOHOST_ADDR: Addr = 32'h1000;
    always_ff {
        if membus.valid && membus.wen == 1 && membus.addr == RISCVTESTS_TOHOST_ADDR {
            if membus.wdata == 1 {
                $display("riscv-tests success!");
            } else {
                $display("riscv-tests failed!");
                $error  ("wdata : %h", membus.wdata);
            }
            $finish();
        }
    }

    // #@@range_begin(always_arb)
    // メモリアクセスを調停する
    always_ff {
        if_reset {
            memarb_last_i     = 0;
            @<b>|memarb_last_iaddr = 0;|
        } else {
            if membus.ready {
                memarb_last_i     = !d_membus.valid;
                @<b>|memarb_last_iaddr = i_membus.addr;|
            }
        }
    }
    // #@@range_end(always_arb)

    always_comb {
        i_membus.ready  = membus.ready && !d_membus.valid;
        i_membus.rvalid = membus.rvalid && memarb_last_i;
    // #@@range_begin(rdata)    
    i_membus.rdata  = if memarb_last_iaddr[2] == 0 {
        membus.rdata[31:0]
    } else {
        membus.rdata[63:32]
    };
    //#@@range_end(rdata)

        d_membus.ready  = membus.ready;
        d_membus.rvalid = membus.rvalid && !memarb_last_i;
        d_membus.rdata  = membus.rdata;

        membus.valid = i_membus.valid | d_membus.valid;
        if d_membus.valid {
            membus.addr  = addr_to_memaddr(d_membus.addr);
            membus.wen   = d_membus.wen;
            membus.wdata = d_membus.wdata;
            membus.wmask = d_membus.wmask;
        } else {
            membus.addr  = addr_to_memaddr(i_membus.addr);
            membus.wen   = 0; // 命令フェッチは常に読み込み
            membus.wdata = 'x;
            membus.wmask = 'x;
        }
    }

    inst mem: memory::<MEM_DATA_WIDTH, 20> (
        clk                     ,
        rst                     ,
        membus                  ,
        FILE_PATH: MEM_FILE_PATH,
    );

    inst c: core (
        clk       ,
        rst       ,
        i_membus  ,
        d_membus  ,
    );
}

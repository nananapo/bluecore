module top (
    clk: input logic,
    rst: input logic,
) {

    import conf::*;

    // memory
    var mem_valid : logic    ;
    var mem_wen   : logic    ;
    var mem_addr  : Addr     ;
    var mem_wdata : UInt64   ;
    var mem_wmask : logic <8>;
    var mem_rvalid: logic    ;
    var mem_rdata : UInt64   ;

    inst datamemory: $sv::memory #(
        DATA_WIDTH: 64,
        ADDR_WIDTH: 16,
    ) (
        clk                           ,
        rst                           ,
        valid : mem_valid             ,
        wen   : mem_wen               ,
        addr  : mem_addr[16 + 3 - 1:3],
        wdata : mem_wdata             ,
        wmask : mem_wmask             ,
        rvalid: mem_rvalid            ,
        rdata : mem_rdata             ,
    );

    var ireq : meminterface_req ;
    var iresp: meminterface_resp;
    var dreq : meminterface_req ;
    var dresp: meminterface_resp;

    var last_req_is_i: logic;

    always_comb {
        ireq.ready = !dreq.valid;
        dreq.ready = 1;

        iresp.valid = last_req_is_i & mem_rvalid;
        iresp.rdata = mem_rdata;

        dresp.valid = !last_req_is_i & mem_rvalid;
        dresp.rdata = mem_rdata;

        mem_valid = ireq.valid | dreq.valid;
        if (dreq.valid) {
            mem_wen   = dreq.wen;
            mem_addr  = dreq.addr;
            mem_wdata = dreq.wdata;
            mem_wmask = dreq.wmask;
        } else {
            mem_wen   = 0;
            mem_addr  = ireq.addr;
            mem_wdata = ireq.wdata;
            mem_wmask = ireq.wmask;
        }
    }

    local RISCVTESTS_EXIT_ADDR    : Addr   = ('h1000) as Addr;
    local RISCVTESTS_WDATA_SUCCESS: UInt64 = 1;

    always_ff (clk, rst) {
        if_reset {
            last_req_is_i = 0;
        } else {
            last_req_is_i = !dreq.valid;

            if (dreq.valid & dreq.wen & dreq.addr == RISCVTESTS_EXIT_ADDR) {
                $display("wdata: %h", dreq.wdata);
                if (dreq.wdata == RISCVTESTS_WDATA_SUCCESS) {
                    $display("riscv-tests: Success!");
                } else {
                    $error("riscv-tests: Fail");
                }
                $finish();
            }
        }
    }

    inst c: core (
        clk    ,
        rst    ,
        ireq   ,
        iresp  ,
        dreq   ,
        dresp  ,
    );
}
